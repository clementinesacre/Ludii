(define "Phase1"
    (move
        (from (handSite Mover))
        (to (sites Empty))
    )
)

(define "Phase2"
    (if ("SameTurn")
        "AddDisk"
        "MoveRing"
    )
)

(define "AddDisk"
    (if
         (is Mover P1)
         (move
             Add (piece (id "Disc0") state:1)
             (to (last From))
         )
         (move
             Add (piece (id "Disc0") state:2)
             (to (last From))
         )
    )
)

(define "MoveRing"
    (forEach Piece "Ring"
        (then (moveAgain))
    )
)

(define "HopCaptureDistanceDiscs"
    (move Hop
        #1
        #2
        (between
            before:(count Rows)
            #3
            after:0
            if:(is In (between) (sites Occupied by:Neutral component:"Disc"))
            (apply (flip (between)))
        )
        (to if:(is Empty (to)))
        #4
    )
)

(define "RingMovements"
    (or
        (move Slide)
        ("HopCaptureDistanceDiscs" (from) Adjacent (range 1 Infinity))
    )
)

//-----------------------------------------------
// Main routine


(game "Yinsh"
    (players 2)
    (equipment {
        (board (rotate 90 (hex 10)))
        (piece "Disc" Neutral (flips 1 2))
        (piece "Ring" Each "RingMovements")
        (hand Each)
    })
    (rules
        (start (place "Ring" "Hand" count:3))

        (play
            (if
                ("HandEmpty" Mover)
                "Phase2"
                "Phase1"
            )
        )

        (end {
            (if (is Line 5 if:(= (state at:(to)) 1)) (result P1 Win))
            (if (is Line 5 if:(= (state at:(to)) 2)) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info {
        (description "Yinsh is a French abstract combinatorial strategy game by Kris Burm released in 2003 and published by Don & Co.")
        (rules "")
        (source "<a href=\"https://www.jeuxdenim.be/jeu-Yinsh?srsltid=AfmBOoqCkHgkw7ILP9atzxYEfuy1raTChToPxVjVafPx7WVwOXbuM0bW\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.di.fc.ul.pt</a>")
        (version "1.3.12")
        (classification "board/space/line")
        (author "Kris Burm")
        (credit "Clémentine Sacré")
        (date "2003")
    })
    (graphics {
        (piece Scale "Disc" 0.8)
        (piece Scale "Ring" 0.8)
        (board Style Board)
        (piece Colour state:1 fillColour:(colour White))
        (piece Colour state:2 fillColour:(colour Black))
    })
)

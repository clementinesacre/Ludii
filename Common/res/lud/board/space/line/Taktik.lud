//-----------------------------------------------
// Main routine

(game "Taktik"
    (players 2)
    (equipment {
        (board (rotate 90 (hex Prism 5 8)))
        (piece "Ball" Each (move Slide (directions {FL FR BL BR Rightward Leftward} of:All)))
        (piece "Square" Neutral)
    })
    (rules
        (start {
            (place "Square0" {"A5" "A4" "A3" "B5" "B6" "C7" "J3" "K4" "K5" "L5" "L6" "L7"})
            (place "Ball1" (coord "A2"))
            (place "Ball1" (coord "D8"))
            (place "Ball1" (coord "K6"))
            (place "Ball1" (coord "J4"))
            (place "Ball2" (coord "B4"))
            (place "Ball2" (coord "C6"))
            (place "Ball2" (coord "L8"))
            (place "Ball2" (coord "I2"))
        })
        (play (forEach Piece))
        (end (if (is Line 4) (result Mover Win)))
    )
)


//---------------------------------------------

(metadata
    (info {
        (description "Inspired by the traditional Tic-Tac-Toe game beloved of high-school students, Taktik is a simple but highly enjoyable game.")
        (aliases {"Brainline"})
        (rules "MOVE - Each stone may slide any number of empty cells on any direction (they cannot jump other stones).
GOAL - Wins the player that aligns his 4 stones.")
        (source "https://www.di.fc.ul.pt/~jpn/gv/taktik.htm")
        (classification "board/space/line")
        (credit "Clémentine Sacré")
        (origin "The author and precise year of this game remain unknown, we just know that it was during the 70s.")
    })
    (graphics {
        (piece Scale "Square" 0.5)
        (piece Colour "Square" fillColour:(colour Black))
        (piece Scale "Ball" 0.6)
        (board Style Board)
    })
)